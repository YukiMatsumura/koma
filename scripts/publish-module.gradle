apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  from android.sourceSets.main.java.srcDirs
  from android.sourceSets.main.kotlin.srcDirs
}

tasks.dokkaHtml.configure {
  outputDirectory.set(file("../documentation/html"))
}

tasks.withType(dokkaHtml.getClass()).configureEach {
  pluginsMapConfiguration.
      set(["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""])
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
  archiveClassifier.set('javadoc')
  from dokkaJavadoc.outputDirectory
}

artifacts {
  archives androidSourcesJar
  archives javadocJar
}

signing {
  useInMemoryPgpKeys(rootProject.ext["signing.keyId"],
      rootProject.ext["signing.key"],
      rootProject.ext["signing.password"],)
  sign publishing.publications
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        // The coordinates of the library, being set from variables that
        // we'll set up later
        groupId PUBLISH_GROUP_ID
        artifactId PUBLISH_ARTIFACT_ID
        version PUBLISH_VERSION

        // Two artifacts, the `aar` (or `jar`) and the sources
        if (project.plugins.findPlugin("com.android.library")) {
          from components.release
        } else {
          from components.java
        }

        artifact androidSourcesJar
        artifact javadocJar

        // Mostly self-explanatory metadata
        pom {
          name = PUBLISH_ARTIFACT_ID
          description =
              '"koma" is an Android library that visualizes the UI rendering performance of your app.'
          url = 'https://github.com/YukiMatsumura/koma'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'YukiMatsumura'
              name = 'Matsumura Yuki'
              email = 'define1394@gmail.com'
            }
          }

          // Version control info - if you're using GitHub, follow the
          // format as seen here
          scm {
            connection = 'scm:git:github.com/YukiMatsumura/koma.git'
            developerConnection = 'scm:git:ssh://github.com/YukiMatsumura/koma.git'
            url = 'https://github.com/YukiMatsumura/koma/tree/main'
          }
        }
      }
    }
  }
}